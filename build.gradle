import org.gradle.plugins.signing.Sign
import javax.swing.*

plugins {
    id 'idea'
    id 'maven-publish'
    id "com.github.ethankhall.semantic-versioning" version "1.1.0"
    id "com.github.hierynomus.license" version "0.12.1"
    id "com.jfrog.bintray" version "1.7.2"
    id "net.saliman.properties" version "1.4.6"
}

project.version.with {
    major = jooqGroovyTestVersionMajor as int
    minor= jooqGroovyTestVersionMinor as int
    patch = jooqGroovyTestVersionPatch as int
    if (project.hasProperty('jooqGroovyTestVersionLabel')) {
        preRelease = jooqGroovyTestVersionLabel
    }
    releaseBuild = Boolean.valueOf(jooqGroovyTestReleaseBuild)
}
ext.jooqGroovyTestVersion = project.version as String
ext.jooqGroovyTestTag = Boolean.valueOf(jooqGroovyTestReleaseBuild) ? "v$ext.jooqGroovyTestVersion" : 'master'

def pomConfig = {
    url "https://github.com/to-be-deleted/jooq-groovy-test"
    developers {
        developer {
            id "siordache"
            name "Serban Iordache"
        }
    }
    scm {
        connection "https://github.com/to-be-deleted/jooq-groovy-test.git"
        developerConnection "https://github.com/to-be-deleted/jooq-groovy-test.git"
        url "https://github.com/to-be-deleted/jooq-groovy-test"
    }
}

apply plugin: 'signing'
repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'application'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.hierynomus.license'

sourceCompatibility = 1.8
group = 'org.beryx'
version = jooqGroovyTestVersion

def defaultEncoding = 'UTF-8'
[compileGroovy, compileTestGroovy]*.options*.encoding = defaultEncoding

license {
    header rootProject.file("license-header.txt")
    skipExistingHeaders true
    ignoreFailures false
}

signing {
    sign configurations.archives
}

signArchives {
    onlyIf { gradle.taskGraph.allTasks.findAll {task -> isPublishTask(task)} }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.12"
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    runtime ('ch.qos.logback:logback-classic:1.1.7')

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.4'
    testCompile 'org.objenesis:objenesis:2.4'
    testCompile('com.athaydes:spock-reports:1.2.13') { transitive = false}
    testCompile 'junit:junit:4.12'
    testCompile('ch.qos.logback:logback-classic:1.1.7')
}

jar {
    manifest {
        attributes 'Implementation-Title': "jooq-groovy-test",
                'Main-Class': 'org.beryx.jooq.JOOQTest',
                'Implementation-Version': jooqGroovyTestVersion
    }
}
mainClassName = 'org.beryx.jooq.JOOQTest'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: classes) {
    classifier "javadoc"
    from groovydoc
}

artifacts {
    archives(sourcesJar, groovydocJar)
}

publishing {
    publications {
        "jooq-groovy-test"(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }
            artifact groovydocJar { classifier "javadoc" }
            groupId 'org.beryx'
            artifactId project.name
            version jooqGroovyTestVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('name', "Module $project.name")
                root.appendNode('description', "The $project.name artifact")
                root.children().last() + pomConfig

                // We cannot configure the licenses in pomConfig due to a name conflict with the 'license' extension defined by the com.github.hierynomus.license plugin
                def licenseNode = root.appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'The Apache Software License, Version 2.0')
                licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenseNode.appendNode('distribution', 'repo')
            }
        }
    }
}

def readPasswordFromConsole(title, prompt) {
    JPanel panel = new JPanel()
    JLabel label = new JLabel(prompt)
    JPasswordField pass = new JPasswordField(24)
    panel.add(label)
    panel.add(pass)
    def options = ["OK", "Cancel"] as Object[]
    int option = JOptionPane.showOptionDialog(null, panel, title,
            JOptionPane.NO_OPTION, JOptionPane.PLAIN_MESSAGE, null, options, null)
    if(option != 0) throw new InvalidUserDataException("Operation cancelled by the user.")
    new String(pass.password)
}

def isPublishTask(task) {
    task.name.startsWith('publish')
}

gradle.taskGraph.whenReady { taskGraph ->
    if (gradle.taskGraph.allTasks.findAll {task -> isPublishTask(task)}) {
        if(!project.hasProperty('signingKeyId') || !project.hasProperty('signingSecretKeyRingFile'))
            throw new InvalidUserDataException("Please configure your signing credentials in gradle-local.properties.")
        def password = readPasswordFromConsole('Please enter your PGP credentials:', 'PGP Private Key Password')
        allprojects { ext."signing.keyId" = signingKeyId }
        allprojects { ext."signing.secretKeyRingFile" = signingSecretKeyRingFile }
        allprojects { ext."signing.password" = password }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : "unknownUser"
    key =  project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : "unknownKey"
    publications = ['jooq-groovy-test']
    pkg {
        repo = 'maven'
        name = 'jooq-groovy-test'
        userOrg = 'to-be-deleted'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/to-be-deleted/jooq-groovy-test.git'

        version {
                name = jooqGroovyTestVersion
                desc = "jooq-groovy-test $jooqGroovyTestVersion"
                released  = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
                vcsTag = jooqGroovyTestVersion
                gpg {
                    sign = true
                }
        }
    }
}
